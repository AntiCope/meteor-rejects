plugins {
	id 'fabric-loom' version '1.10.1'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "https://maven.meteordev.org/releases"}
	maven { url "https://maven.meteordev.org/snapshots" }
	maven { url "https://maven.seedfinding.com/" }
	maven { url "https://maven-snapshots.seedfinding.com/" }
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.duti.dev/releases' }
}

loom {
	accessWidenerPath = file("src/main/resources/meteor-rejects.accesswidener")
}

configurations {
	// configuration that holds jars to include in the jar
	extraLibs
}

dependencies {
	// This will make it work on most platforms. It automatically chooses the right dependencies at runtime.
	extraLibs('dev.duti.acheong:cubiomes:1.22.3') { transitive = false }
	extraLibs('dev.duti.acheong:cubiomes:1.22.3:linux64') { transitive = false }
	extraLibs('dev.duti.acheong:cubiomes:1.22.3:osx') { transitive = false }
	extraLibs('dev.duti.acheong:cubiomes:1.22.3:windows64') { transitive = false }
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation("meteordevelopment:meteor-client:${project.minecraft_version}-SNAPSHOT")
	modCompileOnly "meteordevelopment:baritone:${project.baritone_version}-SNAPSHOT"

	// seed .locate and ore sim
	extraLibs('com.seedfinding:mc_math:851e9d0577dfdca50154e98f1d334bd31c641326') { transitive = false }
	extraLibs('com.seedfinding:mc_seed:55f6242001f7eb4226df4ed0d023f1838a54a99d') { transitive = false }
	extraLibs('com.seedfinding:mc_core:b84ff1c38be399eda096dbde2819258c62f81275') { transitive = false }
	extraLibs('com.seedfinding:mc_noise:dbab3996ea3abff5dd420db53c31d5498afd2fe5') { transitive = false }
	extraLibs('com.seedfinding:mc_biome:17af8cb1110fdc983b7cb2b887d1fb2060e23ee3') { transitive = false }
	extraLibs('com.seedfinding:mc_terrain:a03e440ec5b282e399382f2cc5ad0db91b438d2e') { transitive = false }
	extraLibs('com.seedfinding:mc_feature:755d3611ac1c499c28289ccca5b738af6a5859b7') { transitive = false }
	extraLibs('com.seedfinding:mc_reversal:75aa6ce47a9f53a1aa212765e9830e08f6c86299') { transitive = false }

	// seedcracker api
	implementation (include('com.github.19MisterX98.SeedcrackerX:seedcrackerx-api:2.10.1')) {transitive = false}
//	implementation (include('com.github.19MisterX98.SeedcrackerX:seedcrackerx-api:master-SNAPSHOT')) {transitive = false}

	configurations.implementation.extendsFrom(configurations.extraLibs)
}

processResources {
	inputs.property "version", project.version


	filesMatching("fabric.mod.json") {
		expand "version": project.version,
				"mc_version": project.minecraft_version,
				"gh_hash": (System.getenv("GITHUB_SHA") ?: "")
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
	from {
		configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
tasks.withType(Jar) {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 21
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}
